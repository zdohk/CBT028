PROC 0 LIST SYMLIST CONLIST INPUT(OUTLIST) OUTPUT(OUTLWK) +
       VOL(PL8608) DSN(A.CNTL) TAPE(TAPE) JNAM(TST2#SG$)  +
       PFX1(TST2SSG) PFX2(PUT8608)

/* THIS CLIST BACKS TO TAPE MULTIPLE SYSOUTS HAVING THE SAME JOB NAME
/*  "JNAM ", WHICH ARE IN A TSO-HELD OUTPUT CLASS.
/*
/* THIS CLIST TAKES AS INPUT A LIST OF JOB NUMBERS, AND GENERATES
/* ON "OUTPUT" A JOBSTREAM TO DUMP THE JOBS VIA THE OUTPUT TSO COMMAND
/* TO AN SL TAPE THAT HAS BEEN PREVIOUSLY INITIALIZED.
/*
/* JOB NUMBERS MUST BE IN COLUMNS 1 TO 4, AND LEFT-PADDED WITH ZEROS.
/*
/* AN ASTERISK IN COLUMN 1 CAUSES THE CARD TO BE TREATED AS COMMENT.
/*
/* THE INPUT CARD IS ASSUMED TO BE A MEMBER "OUTLIST" IN A DATASET
/* PREFIX.A.CNTL .  THE OUTPUT IS WRITTEN TO A MEMBER "OUTLWK" IN
/* THAT DATASET.
/*
/*   THE JOB NUMBERS CAN BE GENERATED USING THE TSO STATUS COMMAND.

IF &SYMLIST ¬= THEN CONTROL SYM
IF &CONLIST ¬= THEN CONTROL CON
IF &LIST    ¬= THEN CONTROL LIST
ALLOC F(IN) DA(&DSN(&INPUT)) SH  REU
OPENFILE IN INPUT
/*              12345678901234567890  */
SET &J = &STR(J)
SET &DSNS =
SET &NUMDSNS = 0
SET &EOFG = 0
DO WHILE &EOFG = 0
    ERROR DO
        WRITE
        WRITE END OF FILE ON &DSN(&INPUT).
        CLOSFILE IN
        FREE F(IN)
        SET &EOFG = 1
        RETURN
    END
    GETFILE IN
    ERROR OFF
    IF &EOFG = 0 THEN DO
        IF &SUBSTR(1,&STR(&IN)) ¬= &STR(*) THEN DO
            SET &DSNS = &STR(&DSNS&SUBSTR(1:4,&STR(&IN))
            SET &NUMDSNS = &NUMDSNS + 1
        END
        ELSE DO
            SET &SYSDVAL = &SUBSTR(2:4,&STR(&IN))
            READDVAL IN
            WRITE JOB NUMBER &IN WILL NOT BE BACKED UP.
        END
    END
END
IF &EOFG = 0 THEN DO
    WRITE
    WRITE &DSN(&INPUT) IS EMPTY.  NO JCL GENERATED.
    WRITE
END
ELSE DO
    WRITE GENERATING JCL IN &DSN(&OUTPUT).
    ALLOC F(OUT) DA(&DSN(&OUTPUT)) SH  REU
    OPENFILE OUT OUTPUT
    /*              12345678901234567890
    SET &OUT = &STR(//TST2OP2L JOB ,'TECH.SUPP---IBMUSER',CLASS=M,NOTIF-
Y=TST2SSG,)
    PUTFILE OUT
    SET &OUT = &STR(// MSGLEVEL=(1,1),MSGCLASS=T TYPRUN=HOLD           -
    )
    PUTFILE OUT
    SET &OUT = &STR(//*                                                -
    )
    PUTFILE OUT
    SET &OUT = &STR(//*************************************************-
*//)
    PUTFILE OUT
    SET &OUT = &STR(//*                                                -
*// )
    PUTFILE OUT
    SET &OUT = &STR(//* GENERATED JCL FOR OUTPUT DUMP OF SMP JOBS.     -
*// )
    PUTFILE OUT
    SET &OUT = &STR(//*                                                -
*// )
    PUTFILE OUT
    SET &OUT = &STR(//*************************************************-
*//)
    PUTFILE OUT
    SET &OUT = &STR(//*                   )
    PUTFILE OUT
    SET &OUT = &STR(//LIST    EXEC PGM=IKJEFT01,DYNAMNBR=50,REGION=4096-
K)
    PUTFILE OUT
    SET &OUT = &STR(//STEPLIB  DD  DSN=TST.TSO.CMDLIB,DISP=SHR     )
    PUTFILE OUT
    SET &OUT=  &STR(//SYSPRINT DD  SYSOUT=*)
    PUTFILE OUT
    SET &OUT=  &STR(//SYSTSPRT DD  SYSOUT=*)
    PUTFILE OUT
    SET &OUT=  &STR(//SYSTERM  DD  SYSOUT=*)
    PUTFILE OUT
    SET &IX = 1
    SET &IZ = 1
    SET &L = &LENGTH(&STR(&DSNS))
    DO WHILE &IX <= &L
        SET &IY = &IX + 3
        SET &IA = &IZ - 1
        SET &SYSDVAL = &SUBSTR(&IX:&IY,&STR(&DSNS))
        READDVAL IN
        SET &OUT = &STR(//OUTP&IZ  DD  DSN=&PFX1..&PFX2..&J&SYSDVAL,DIS-
P=(,PASS), )
        PUTFILE OUT
        SET &OUT = &STR(//        LABEL=(&IZ,SL,EXPDT=98000),UNIT=&TAPE-
,)
        PUTFILE OUT
        SET &OUT = &STR(//        VOL=(,RETAIN,SER=&VOL),)
        PUTFILE OUT
        SET &OUT = &STR(//        DCB=(RECFM=FBA,LRECL=133,BLKSIZE=3059-
)      )
        PUTFILE OUT
        SET &IX = &IX + 4
        SET &IZ = &IZ + 1
    END
    SET &OUT = &STR(//SYSTSIN  DD  DATA,DLM='/$')
    PUTFILE OUT
    SET &GROOT = &STR(/)&STR(*)
    SET &OUT = &STR(&GROOT THIS JCL CREATED ON &SYSDATE AT &SYSTIME..)
    PUTFILE OUT
    SET &L = &LENGTH(&STR(&DSNS))
    SET &IX = 1
    DO WHILE &IX <= &L
        SET &IY = &IX + 3
        SET &IA = &IZ - 1
        SET &SYSDVAL = &SUBSTR(&IX:&IY,&STR(&DSNS))
        READDVAL IN
        /*              12345678901234567890
        SET &OUT = &STR( OUT &JNAM(&J&SYSDVAL) HOLD KEEP PRINT('&PFX1..-
&PFX2..&J&SYSDVAL')
        PUTFILE OUT
        SET &IX = &IX + 4
    END
    SET &OUT = &STR(/$                          )
    PUTFILE OUT
    SET &OUT = &STR(//RESET   EXEC PGM=IEFBR14,REGION=768K)
    PUTFILE OUT
    SET &OUT = &STR(//MAP     EXEC PGM=TAPEMAP,REGION=768K)
    PUTFILE OUT
    SET &OUT = &STR(//SYSPRINT DD  SYSOUT=*)
    PUTFILE OUT
    SET &OUT = &STR(//SYSPRNT2 DD  SYSOUT=*)
    PUTFILE OUT
    SET &OUT = &STR(//SYSUT1   DD  UNIT=&TAPE,DISP=OLD,   )
    PUTFILE OUT
    SET &OUT = &STR(//     VOL=(,RETAIN,,,SER=&VOL),     )
    PUTFILE OUT
    SET &OUT = &STR(//     LABEL=(1,BLP,EXPDT=98000)                )
    PUTFILE OUT
    CLOSFILE OUT
    FREE F(OUT)
END
