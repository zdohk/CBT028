PROC 0 LIST SYMLIST CONLIST INPUT(DATASET3) OUTPUT(UNLDTS) +
       VOL(WORK01) DSN(B.CNTL) OUN(562) OVSER(SBGCBT) +
       STEPL(IBMUSER.APF.LOADLIB)

/* THIS CLIST TAKES AS INPUT A LIST OF DATASETS, AND GENERATES
/* ON OUTPUT A JOBSTREAM TO TAKE IEBCOPIES OF THESE DATASETS.
/* REWRITTEN TO GENERATE JCL FROM AN UNLIMITEDLY LONG LIST OF
/*   DATASETS.  OTHERWISE SIMILAR TO GENUNLD.

IF &SYMLIST ¬= THEN CONTROL SYM
IF &CONLIST ¬= THEN CONTROL CON
IF &LIST    ¬= THEN CONTROL LIST
WRITE &SYSDATE &SYSTIME ***   GENERATION OF UNLOAD JCL    ***
WRITE &SYSDATE &SYSTIME ***    TO CREATE TAPE BACKUP      ***
WRITE
WRITE &SYSDATE &SYSTIME BEGIN JOURNAL OF OPERATIONS
ALLOC F(IN) DA(&DSN(&INPUT)) SH  REU
OPENFILE IN INPUT
/*              12345678901234567890  */
IF &EOFG = 0 THEN DO
    WRITE
    WRITE &SYSDATE &SYSTIME &DSN(&INPUT) IS EMPTY.  NO JCL GENERATED.
    WRITE
END
ELSE DO
    WRITE &SYSDATE &SYSTIME GENERATING JCL IN &DSN(&OUTPUT).
    ALLOC F(OUT) DA(&DSN(&OUTPUT)) SH  REU
    OPENFILE OUT OUTPUT
    /*              12345678901234567890
    SET &OUT = &STR(//IBMUSERD JOB ,'TECH.SUPP---IBMUSER',CLASS=B,NOTIF-
Y=&SYSUID,)
    PUTFILE OUT
    SET &OUT = &STR(// MSGLEVEL=(1,1),MSGCLASS=X TYPRUN=HOLD           -
    )
    PUTFILE OUT
    SET &OUT = &STR(//*                                                -
    )
    PUTFILE OUT
    SET &OUT = &STR(//*************************************************-
*//)
    PUTFILE OUT
    SET &OUT = &STR(//*                                                -
*// )
    PUTFILE OUT
    SET &OUT = &STR(//* GENERATED JCL FOR IEBCOPY UNLOAD OF DATASETS   -
*// )
    PUTFILE OUT
    SET &OUT = &STR(//*                                                -
*// )
    PUTFILE OUT
    SET &OUT = &STR(//*************************************************-
*//)
    PUTFILE OUT
    SET &OUT = &STR(//*                   )
    PUTFILE OUT
    SET &OUT = &STR(//LIST    EXEC PGM=IKJEFT01,DYNAMNBR=10,REGION=5000-
K)
    PUTFILE OUT
    SET &OUT = &STR(//STEPLIB  DD  DSN=&STEPL,DISP=SHR )
    PUTFILE OUT
    SET &OUT = &STR(//SYSTSPRT DD  DSN=B.INFO.ABOUT.TAPE,DISP=(,PASS), -
 )
    PUTFILE OUT
    SET &OUT = &STR(//       LABEL=EXPDT=98000,UNIT=&OUN,VOL=(,RETAIN,S-
ER=&OVSER), )
    PUTFILE OUT
    SET &OUT = &STR(//       DCB=(RECFM=VBA,LRECL=137,BLKSIZE=32756) )
    PUTFILE OUT
    SET &OUT = &STR(//SYSTSIN  DD  DATA,DLM='/$')
    PUTFILE OUT
    SET &GROOT = &STR(/)&STR(*)
    SET &OUT = &STR(&GROOT THIS JCL CREATED ON &SYSDATE AT &SYSTIME..)
    PUTFILE OUT
    SET &OUT = TIME
    PUTFILE OUT
/*
SET &NUMDSNS = 0
SET &EOFG = 0
DO WHILE &EOFG = 0
    ERROR DO
        WRITE &SYSDATE &SYSTIME END OF FILE ON &DSN(&INPUT).
        CLOSFILE IN
        FREE F(IN)
        SET &EOFG = 1
        RETURN
    END
    GETFILE IN
    ERROR OFF
    IF &EOFG = 0 THEN DO
        IF &SUBSTR(1,&STR(&IN)) ¬= &STR(*) THEN DO
        SET &NUMDSNS = &NUMDSNS + 1
        SET &OUT = &STR(&GROOT)
        PUTFILE OUT
        SET &OUT = &STR(&GROOT                        TAPE DATASET &NUM-
DSNS.:)
        PUTFILE OUT
        SET &SYSDVAL = &SUBSTR(1:44,&STR(&IN))
        READDVAL TEMP
        SET &OUT = &STR(PDS '&TEMP')
        PUTFILE OUT
        SET &OUT = &STR(DISPLAY)
        PUTFILE OUT
        SET &OUT = &STR(U)
        PUTFILE OUT
        SET &OUT = &STR(ATTRIB : SHORT)
        PUTFILE OUT
        SET &OUT = &STR(END )
        PUTFILE OUT
        SET &OUT = &STR(LISTD '&TEMP' ST H)
        PUTFILE OUT
        END
        ELSE DO
            SET &SYSDVAL = &SUBSTR(2:44,&STR(&IN))
            READDVAL IN
            WRITE ** - ** DATASET &IN WILL NOT BE BACKED UP.
        END
    END
END
    SET &OUT = &STR(/$)
    PUTFILE OUT
    SET EOFG = 0
    /*              1234567890123456
    SET &OUT = &STR(//COPYL   EXEC PGM=IEBGENER)
    PUTFILE OUT
    SET &OUT = &STR(//SYSPRINT DD  SYSOUT=*)
    PUTFILE OUT
    SET &OUT = &STR(//SYSUT1   DD  DSN=B.INFO.ABOUT.TAPE,DISP=(SHR,PASS-
),UNIT=&OUN, )
    PUTFILE OUT
    SET &OUT = &STR(//    LABEL=EXPDT=98000,VOL=(,RETAIN,,,REF=*.LIST.S-
YSTSPRT), )
    PUTFILE OUT
    SET &OUT = &STR(//       DCB=(RECFM=VBA,LRECL=137,BLKSIZE=32756) )
    PUTFILE OUT
    SET &OUT = &STR(//SYSUT2   DD  SYSOUT=*
    PUTFILE OUT
    SET &OUT = &STR(//SYSIN    DD  DUMMY
    PUTFILE OUT
    SET &OUT=  &STR(//BACKUP  EXEC PGM=IEBCOPY,REGION=5000K)
    PUTFILE OUT
    SET &OUT=  &STR(//SYSPRINT DD  SYSOUT=*)
    PUTFILE OUT
    SET &OUT = &STR(//SYSUT3   DD  UNIT=SYSALLDA,SPACE=(CYL,(5,5))  )
    PUTFILE OUT
    SET &OUT = &STR(//SYSUT4   DD  UNIT=SYSALLDA,SPACE=(CYL,(5,5))  )
    PUTFILE OUT
/*
WRITE &SYSDATE &SYSTIME STARTING TO GENERATE IEBCOPY DDNAMES.
ALLOC F(IN) DA(&DSN(&INPUT)) SH  REU
OPENFILE IN INPUT
SET &NUMD = &NUMDSNS
SET &NUMDSNS = 0
SET &LBL = 1
SET &EOFG = 0
ERROR OFF
DO WHILE &NUMDSNS < &NUMD
    ERROR DO
        WRITE
        WRITE &SYSDATE &SYSTIME SECOND END OF FILE ON &DSN(&INPUT).
        CLOSFILE IN
        FREE F(IN)
        SET &EOFG = 1
        RETURN
    END
    GETFILE IN
    ERROR OFF
    IF &EOFG = 0 THEN DO
        IF &SUBSTR(1,&STR(&IN)) ¬= &STR(*) THEN DO
        SET &NUMDSNS = &NUMDSNS + 1
        SET &LBL = &LBL + 1
/*
        SET &SYSDVAL = &STR(&IN)
        READDVAL TEMP
        SET &OUT = &SUBSTR(1:11,&STR(&STR(//I&NUMDSNS             ))
        SET &OUT = &STR(&SUBSTR(1:11,&OUT)DD  DISP=SHR,DSN=&TEMP)
        PUTFILE OUT
        SET &OUT = &SUBSTR(1:11,&STR(&STR(//O&NUMDSNS             ))
        SET &OUT = &STR(&SUBSTR(1:11,&OUT)DD  DSN=B.&TEMP,)
        PUTFILE OUT
        SET &OUT = &STR(//             DISP=(,PASS),LABEL=(&LBL,RETPD=9-
0),)
        PUTFILE OUT
        SET &OUT = &STR(//    VOL=(,RETAIN,,,REF=*.LIST.SYSTSPRT),UNIT=-
&OUN )
        PUTFILE OUT
        /*               1234567890123456789012345678901234567890123456-
7890123456789012345678901234567890)
        END
    END
END
    SET &OUT = &STR(//SYSIN    DD  *)
    PUTFILE OUT
    SET &IY = 1
    DO WHILE &IY <= &NUMD
        SET &OUT = &STR( C I=I&IY,O=O&IY,LIST=NO)
        PUTFILE OUT
        SET &IY = &IY + 1
    END
    /*              123456789012345678901234567890123456789012345678901-
23456789012345678901234567890)
    SET &OUT = &STR(//RESET   EXEC PGM=IEFBR14,REGION=2048K)
    PUTFILE OUT
    SET &OUT = &STR(//MAP     EXEC PGM=TAPEMAP,REGION=5000K)
    PUTFILE OUT
    SET &OUT = &STR(//STEPLIB  DD  DISP=SHR,DSN=&STEPL )
    PUTFILE OUT
    SET &OUT = &STR(//SYSUT1   DD  UNIT=&OUN,DISP=OLD,    )
    PUTFILE OUT
    SET &OUT = &STR(//     VOL=(,RETAIN,,,SER=&OVSER),     )
    PUTFILE OUT
    SET &OUT = &STR(//     LABEL=(1,BLP,EXPDT=98000)                )
    PUTFILE OUT
    SET &OUT = &STR(//SYSPRINT DD  SYSOUT=*)
    PUTFILE OUT
    SET &OUT = &STR(//SYSPRNT2 DD  SYSOUT=*)
    PUTFILE OUT
    CLOSFILE OUT
    FREE F(OUT)
WRITE &SYSDATE &SYSTIME NORMAL END OF JCL GENERATION.
END
