PROC 0 LIST SYMLIST CONLIST INPUT(DATASEG) OUTPUT(UNLDGEN) +
       VOL(PTF004) DSN(A.CNTL)

/* THIS CLIST TAKES AS INPUT A LIST OF DATASETS, AND GENERATES
/* ON OUTPUT A JOBSTREAM TO TAKE  DSCOPIES OF THESE DATASETS.

IF &SYMLIST ¬= THEN CONTROL SYM
IF &CONLIST ¬= THEN CONTROL CON
IF &LIST    ¬= THEN CONTROL LIST
ALLOC F(IN) DA(&DSN(&INPUT)) SH  REU
OPENFILE IN INPUT
/*              12345678901234567890  */
SET &DSNS =
SET &NUMDSNS = 0
SET &EOFG = 0
DO WHILE &EOFG = 0
    ERROR DO
        WRITE
        WRITE END OF FILE ON &DSN(&INPUT).
        CLOSFILE IN
        FREE F(IN)
        SET &EOFG = 1
        RETURN
    END
    GETFILE IN
    ERROR OFF
    IF &EOFG = 0 THEN DO
        IF &SUBSTR(1,&STR(&IN)) ¬= &STR(*) THEN DO
            SET &DSNS = &STR(&DSNS&SUBSTR(1:44,&STR(&IN))
            SET &NUMDSNS = &NUMDSNS + 1
        END
        ELSE DO
            SET &SYSDVAL = &SUBSTR(2:44,&STR(&IN))
            READDVAL IN
            WRITE DATASET &IN WILL NOT BE BACKED UP.
        END
    END
END
IF &EOFG = 0 THEN DO
    WRITE
    WRITE &DSN(&INPUT) IS EMPTY.  NO JCL GENERATED.
    WRITE
END
ELSE DO
    WRITE GENERATING JCL IN &DSN(&OUTPUT).
    ALLOC F(OUT) DA(&DSN(&OUTPUT)) SH  REU
    OPENFILE OUT OUTPUT
    /*              12345678901234567890
    SET &OUT = &STR(//TST2TP2C JOB ,'TECH.SUPP---IBMUSER',CLASS=M,NOTIF-
Y=TST2SSG,)
    PUTFILE OUT
    SET &OUT = &STR(// MSGLEVEL=(1,1),MSGCLASS=T TYPRUN=HOLD           -
    )
    PUTFILE OUT
    SET &OUT = &STR(//*                                                -
    )
    PUTFILE OUT
    SET &OUT = &STR(//*************************************************-
*//)
    PUTFILE OUT
    SET &OUT = &STR(//*                                                -
*// )
    PUTFILE OUT
    SET &OUT = &STR(//* GENERATED JCL FOR SEQCPY UNLOAD OF DATASETS    -
*// )
    PUTFILE OUT
    SET &OUT = &STR(//*                                                -
*// )
    PUTFILE OUT
    SET &OUT = &STR(//*************************************************-
*//)
    PUTFILE OUT
    SET &OUT = &STR(//*                   )
    PUTFILE OUT
    SET &OUT = &STR(//LIST    EXEC PGM=IKJEFT01,DYNAMNBR=10,REGION=2048-
K)
    PUTFILE OUT
    SET &OUT = &STR(//STEPLIB  DD  DSN=TST.TSO.CMDLIB,DISP=SHR     )
    PUTFILE OUT
    SET &OUT = &STR(//SYSTSPRT DD  DSN=B.INFO.ABOUT.TAPE,DISP=(,PASS), -
 )
    PUTFILE OUT
    SET &OUT = &STR(//             LABEL=EXPDT=98000,UNIT=TAPE,VOL=(,RE-
TAIN,,,SER=&VOL),)
    PUTFILE OUT
    SET &OUT = &STR(//             DCB=(RECFM=VBA,LRECL=137,BLKSIZE=327-
56) )
    PUTFILE OUT
    SET &OUT = &STR(//SYSTSIN  DD  DATA,DLM='/$')
    PUTFILE OUT
    SET &GROOT = &STR(/)&STR(*)
    SET &OUT = &STR(&GROOT THIS JCL CREATED ON &SYSDATE AT &SYSTIME..)
    PUTFILE OUT
    SET &OUT = TIME
    PUTFILE OUT
    SET &L = &LENGTH(&STR(&DSNS))
    SET &IX = 1
    SET &IZ = 2
    DO WHILE &IX <= &L
        SET &IY = &IX + 43
        SET &OUT = &STR(&GROOT)
        PUTFILE OUT
        SET &OUT = &STR(&GROOT                        TAPE DATASET &IZ.-
:)
        PUTFILE OUT
        SET &SYSDVAL = &SUBSTR(&IX:&IY,&STR(&DSNS))
        READDVAL TEMP
        SET &OUT = &STR(LISTC ENT('&TEMP') ALL)
        PUTFILE OUT
        SET &IX = &IX + 44
        SET &IZ = &IZ + 1
    END
    /*              1234567890123456
    SET &OUT = &STR(/$)
    PUTFILE OUT
    SET &OUT = &STR(//COPYL   EXEC PGM=SEQCPY)
    PUTFILE OUT
    SET &OUT = &STR(//SYSPRINT DD  SYSOUT=*)
    PUTFILE OUT
    SET &OUT = &STR(//IN       DD  DSN=B.INFO.ABOUT.TAPE,DISP=(SHR,PASS-
),UNIT=TAPE,)
    PUTFILE OUT
    SET &OUT = &STR(//    LABEL=EXPDT=98000,VOL=(,RETAIN,,,SER=&VOL))
    PUTFILE OUT
    SET &OUT = &STR(//OUT      DD  SYSOUT=*)
    PUTFILE OUT
    SET &OUT=  &STR(//BACKUP  EXEC PGM=SEQCPY,REGION=3072K)
    PUTFILE OUT
    SET &OUT=  &STR(//SYSPRINT DD  SYSOUT=*)
    PUTFILE OUT
    SET &IX = 1
    SET &IZ = 2
    DO WHILE &IX <= &L
        SET &IY = &IX + 43
        SET &IA = &IZ - 1
        SET &NA = &STR(&IA)
        IF &IA < 100 THEN SET &NA = &STR(0&IA)
        IF &IA < 10 THEN SET &NA = &STR(00&IA)
        SET &SYSDVAL = &SUBSTR(&IX:&IY,&STR(&DSNS))
        READDVAL IN
        /*              12345678901234567890
        SET &OUT = &STR(//IN&NA       )
        SET &OUT = &STR(&SUBSTR(1:11,&OUT)DD  DSN=&IN,DISP=SHR)
        PUTFILE OUT
        SET &OUT = &STR(//OUT&NA       )
        SET &OUT = &STR(&SUBSTR(1:11,&OUT)DD  DSN=B.&IN,)
        PUTFILE OUT
        SET &OUT = &STR(//             DISP=(,PASS),LABEL=(&IZ,EXPDT=98-
000),)
        PUTFILE OUT
        SET &OUT = &STR(//    VOL=(,RETAIN,,,SER=&VOL),UNIT=TAPE)
        PUTFILE OUT
        SET &IX = &IX + 44
        SET &IZ = &IZ + 1
    END
    SET &OUT = &STR(//RESET   EXEC PGM=IEFBR14,REGION=768K)
    PUTFILE OUT
    SET &OUT = &STR(//MAP     EXEC PGM=TAPEMAP,REGION=768K,PARM=NOCHECK)
    PUTFILE OUT
    SET &OUT = &STR(//SYSPRINT DD  SYSOUT=*)
    PUTFILE OUT
    SET &OUT = &STR(//SYSPRNT2 DD  SYSOUT=*)
    PUTFILE OUT
    SET &OUT = &STR(//SYSUT1   DD  UNIT=TAPE,DISP=OLD,              )
    PUTFILE OUT
    SET &OUT = &STR(//     VOL=(,RETAIN,,,SER=&VOL),                )
    PUTFILE OUT
    SET &OUT = &STR(//     LABEL=(1,BLP,EXPDT=98000)                )
    PUTFILE OUT
    CLOSFILE OUT
    FREE F(OUT)
END
